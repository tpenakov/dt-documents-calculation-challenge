plugins {
    id 'java'
    id 'maven-publish'
    id 'org.springframework.boot' version "${springBootVersion}"
    id 'jacoco'
}

repositories {
    mavenLocal()
    maven {
        url = uri('https://repo.maven.apache.org/maven2/')
    }
}

dependencies {
    implementation project(":core")
    implementation project(":csv")
    implementation project(":business")

    //from openapi generator start
    implementation "org.springframework.boot:spring-boot-starter-web:${springBootVersion}"
    implementation "org.springframework.data:spring-data-commons:${springBootVersion}"
    implementation 'org.springdoc:springdoc-openapi-ui:1.6.8'
    implementation 'com.google.code.findbugs:jsr305:3.0.2'
    implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:2.13.3'
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.13.3'
    implementation 'org.openapitools:jackson-databind-nullable:0.2.2'
    implementation "org.springframework.boot:spring-boot-starter-validation:${springBootVersion}"
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.13.3'
    //from openapi generator end

    implementation "org.projectlombok:lombok:${lombokVersion}"
    implementation "io.vavr:vavr:${vavrVersion}"
    implementation "org.apache.commons:commons-lang3:${apacheCommonsLangVersion}"
    implementation "org.apache.commons:commons-collections4:${apacheCommonsCollectionVersion}"

    implementation "org.projectlombok:lombok:${lombokVersion}"
    annotationProcessor "org.projectlombok:lombok:${lombokVersion}"

    testImplementation project(":test")
    testImplementation group: 'commons-io', name: 'commons-io', version: '2.11.0'
    testImplementation "org.springframework.boot:spring-boot-starter-test:${springBootVersion}"
    testAnnotationProcessor "org.projectlombok:lombok:${lombokVersion}"
}

group = 'design.technologies.api'
version = '1.0.0'
description = 'design-technologies-api'
java.sourceCompatibility = JavaVersion.VERSION_1_8

publishing {
    publications {
        maven(MavenPublication) {
            groupId = 'design.technologies.api'
            artifactId = 'api'
            version = '00.00.01-SNAPSHOT'

            from(components.java)
        }
    }
}

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport // report is always generated after tests run
}

jacocoTestReport {
    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: [
                    '**/model/**',
                    '**/generated/**',
                    '**/OpenApiGeneratorApplication*',
                    '**/RFC3339DateFormat*',
            ])
        }))
    }
}
